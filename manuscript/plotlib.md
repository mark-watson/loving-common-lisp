# Plotting Data {#plotlib}

We will use Zach Beane's [vecto library](http://xach.com/lisp/vecto/) for plotting data with the results written to files. Ideally we would like to have interactive plotting capability but for the purposes of this book I need to support the combinations of all Common Lisp implementations on multiple operating systems. Interactive plotting libraries are usually implementation and OS dependent. We will use the **plotlib** example we develop in the later chapter [Backpropagation Neural Networks](#backprop).

## Implementing the Library

The examples here are all contained in the directory **src/plotlib** and is packaged as a Quicklisp loadable library. This library will be used in later chapters.

When I work on my macOS laptop, I leave the output graphics file open in the Preview App and whenever I rerun a program producing graphics in the REPL, making the preview App window active refreshes the graphics display.

{width: "80%"}
![PNG file generated by running plotlib test](images/test-plotlib.png)

The following listing shows the file *plotlib.lisp* that is a simple wrapper for the *vecto* Common Lisp plotting library. Please note that I only implemented wrappers for *vecto* functionality that I need for later examples in this book, so the following code is not particularly general but should be easy enough for you to extend for the specific needs of your projects.

{lang="lisp"}
~~~~~~~~
;; Misc. plotting examples using the vecto library

(ql:quickload :vecto) ;; Zach Beane's plotting library
(defpackage #:plotlib
  (:use #:cl #:vecto))

(in-package #:plotlib)

;; the coordinate (0,0) is the lower left corner of the plotting area.
;; Increasing the y coordinate is "up page" and increasing x is "to the right"

;; fills a rectangle with a gray-scale value
(defun plot-fill-rect (x y width height gscale) ; 0 < gscale < 1
  (set-rgb-fill gscale gscale gscale)
  (rectangle x y width height)
  (fill-path))

;; plots a frame rectangle
(defun plot-frame-rect (x y width height)
  (set-line-width 1)
  (set-rgb-fill 1 1 1)
  (rectangle x y width height)
  (stroke))

(defun plot-line(x1 y1 x2 y2)
  (set-line-width 1)
  (set-rgb-fill 0 0 0)
  (move-to x1 y1)
  (line-to x2 y2)
  (stroke))

(defun plot-string(x y str)
  (let ((font (get-font "OpenSans-Regular.ttf")))
    (set-font font 15)
    (set-rgb-fill 0 0 0)
    (draw-string x y str)))

(defun plot-string-bold(x y str)
  (let ((font (get-font "OpenSans-Bold.ttf")))
    (set-font font 15)
    (set-rgb-fill 0 0 0)
    (draw-string x y str)))


(defun test-plotlib (file)
  (with-canvas (:width 90 :height 90)
    (plot-fill-rect 5 10 15 30 0.2) ; black
    (plot-fill-rect 25 30 30 7 0.7) ; medium gray
    (plot-frame-rect 10 50 30 7)
    (plot-line 90 5 10 5)
    (plot-string 10 65 "test 1 2 3")
    (plot-string-bold 10 78 "Hello")
    (save-png file)))

;;(test-plotlib "test-plotlib.png")
~~~~~~~~

This plot library is used in later examples in the chapters on search, backpropagation neural networks and Hopfield neural networks. I prefer using implementation and operating specific plotting libraires for generating interactive plots, but the advantage of writing plot data to a file using the **vecto** library is that the code is portable across operating systems and Common Lisp implementations.

## Packaging as a Quicklisp Project

The two files **src/plotlib/plotlib.asd** **src/plotlib/package.lisp** configure the library. The file **package.lisp** defines the required library **vecto** and lists the functions that are publicly exported from the library:

{lang="lisp",linenos=off}
~~~~~~~~
(defpackage #:plotlib
  (:use #:cl #:vecto)
  (:export save-png plot-fill-rect plot-frame-rect
     plot-size-rect plot-line plot-string plot-string-bold
     pen-width))
~~~~~~~~

To run the test function provided with this library you load the library and preface exported function names with the package name **plotlib:** as in this example:

{lang="lisp",linenos=off}
~~~~~~~~
(ql:quickload "plotlib")
(plotlib::test-plotlib "test-plotlib.png")
~~~~~~~~

In addition to a **package.lisp** file we also use a file with the extension **.asd**

{lang="lisp",linenos=off}
~~~~~~~~
(asdf:defsystem #:plotlib
  :description "Describe plotlib here"
  :author "mark.watson@gmail.com"
  :license "Apache 2"
  :depends-on (#:vecto)
  :components ((:file "package")
               (:file "plotlib")))
~~~~~~~~

If you have specified a dependency that is not already downloaded to your computer, Quicklisp will install the dependency for you.